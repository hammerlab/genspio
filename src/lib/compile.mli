(** Compilers of the {!EDSL.t} values. *)

(** {3 Pretty-printing Output} *)

val pp_hum: Format.formatter -> 'a EDSL.t -> unit
(** Pretty print a Genspio expression with the {!Format} module. *)

val to_string_hum: 'a EDSL.t -> string
(** Pretty print a Genspio expression to [string]. *)


(** {3 Compilation to POSIX Shell Scripts} *)

(** Compiler from {!EDSL.t} to POSIX shell scripts. *)
module To_posix : sig

  type compilation_error = Language.compilation_error = {

    error: [
      | `No_fail_configured of string (** Argument of fail. *)
      | `Max_argument_length of string (** Incriminated argument. *)
      | `Not_a_c_string of string (** The actual problematic string. *)
    ];
    (** Error description. *)

    code: string option;
    (** Chunk of relevant, pretty-printed EDSL code. *)

    comment_backtrace: string list;
    (** Stack of `Comment` constructs at the point of the error. *)
  }
  (** The potential compilation error. *)

  val pp_error : Format.formatter -> compilation_error -> unit
  (** Printer for error values. *)

  val error_to_string : compilation_error -> string
  (** Convenience display of error values. *)

  type parameters = {

    style : [ `Multi_line | `One_liner ];
    (** The kind of script to output: in one-liners sequences are
        separated with [";"], in multi-line scripts, sequences are
        separated with new lines. *)

    max_argument_length : int option;
    (** A limit on the length of the literal command line arguments
        generated by the compiler.

        - [None] means “do not check.”
        - The default value for is [Some 100_000], meaning that ≥
          100 000 B arguments will make the compiler fail with an
          exception.  *)


    fail_with :
      [ `Kill of string | `Nothing | `Trap_and_kill of int * string ];
    (** How to implement the [EDSL.fail] construct (which appears also
        internally e.g. when {!EDSL.to_c_string} fails.).

        - [`Nothing]: the compiler will fail to compile [fail] constructs.
        - [`Kill signal_name]: the compiler will store the “toplevel”
          process id of the script and {!EDSL.fail} will be trying to
          kill the script with the signal [signal_name]
          (e.g. ["USR2"]).
        - [`Trap_and_kill (exit_status, signal_name)]: the
          {!EDSL.fail} construct will kill the script with
          [signal_name] {b and} the signal will be caught with the
          POSIX ["trap"] command in order to exit with [exit_status].
    *)
  }
  (** Configuration of the compilation to POSIX shell scripts. *)

  val one_liner : parameters
  (** The default parameters for one-liners: {[
        {
          style = `One_liner;
          max_argument_length = Some 100_000;
          fail_with = `Trap_and_kill (78, "USR2");
        }]} *)

  val multi_line : parameters
  (** The default parameters for multi-liners (similar to {!one_liner}). *)

  val default_options : parameters
  (** The default value for [?option] in {!string}, which is {!one_liner}. *)

  val string : ?options:parameters -> 'a EDSL.t ->
    (string, compilation_error) result
  (** Compile a Genspio expression to a POSIX shell “phrase”
      (one-liner or multi-line) according to the [?options] (see
      {!parameters}).
  *)

end


(** {3 Legacy API}

These functions are here for backwards compatibility, please use now
the {!To_posix} module.

*)

val default_max_argument_length: int option
(** See argument [?max_argument_length] of {!to_one_liner}. *)

val to_one_liner :
  ?max_argument_length:int option ->
  ?no_trap:bool ->
  'a EDSL.t ->  string
(** Compile a Genspio expression to a single-line POSIX shell command.

    The shell command starts by using ["trap"] to allow the script to
    abort thorugh the {!EDSL.fail} construct; one can avoid this setup
    with [~no_trap:true]

    If [~no_trap:true] is used and the script used the {!EDSL.fail}
    construct, [to_one_liner] fails with an exception.
    {[
       utop # Genspio.Compile.to_one_liner ~no_trap:true Genspio.EDSL.(seq [ eprintf (string "Hello\\n") []; fail ]);;
       Exception: Failure
       "Die command not set: you cannot use the `fail` construct together with the `~no_trap:true` option (error message was: \"EDSL.fail called\")".
    ]}
    
    The default value for [max_argument_length] is
    {!default_max_argument_length} ([Some 100_000]); it is a limit on
    the length of the literal command line arguments generated by the compiler.
    [None] means “do not check.”

    If the compilation fails, the function raises an [Failure]
    exception containing the error message.
*)

val to_many_lines :
  ?max_argument_length:int option ->
  ?no_trap:bool ->
  'a EDSL.t ->  string
(** Compile a Genspio expression to a multi-line POSIX shell script,
    slightly more readable than {!to_one_liner}.
*)
